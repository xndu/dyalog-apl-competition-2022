DDN←{
    month ←'january' 'february' 'march' 'april' 'may' 'june'
    month,←'july' 'august' 'september' 'october' 'november' 'december'
    months←↑3↑¨month
    monthl←≢¨month
    monthd←31 28 31 30 31 30 31 31 30 31 30 31
    dow   ←'monday' 'tuesday' 'wednesday' 'thursday' 'friday' 'saturday' 'sunday'
    dows  ←↑3↑¨dow
    dowl  ←≢¨dow
    ord   ←'st' 'nd' 'rd' 'th'
    ords  ←⊃¨ord
    half  ←'am' 'pm'
    halfs ←⊃¨half
    parse ←⍺ {
        ''≡⍺:0 2⍴⍬
        ~'_YMDWOPmdhswyotp'∊⍨⊃⍺:⍺ ∇⍥(1∘↓) ⍵
        token rest←4(↑{⍺⍵}↓)⍺
        'YYYY'                     ≡  token:(1 2⍴'Y' (⍎4↑⍵))⍪rest∇⍵↓⍨4
        'MMMM' 'Mmmm' 'mmmm' '_mmm'∊⍨⊂token:(1 2⍴'M' num)   ⍪rest∇⍵↓⍨monthl[num←months⍳⎕C 3↑⍵]
        'WWWW'                     ≡  token:(1 2⍴'Yw'(⍎4↑⍵))⍪rest∇⍵↓⍨4
        'DDDD' 'Dddd' 'dddd' '_ddd'∊⍨⊂token:(1 2⍴'Dw'num)   ⍪rest∇⍵↓⍨dowl[num←dows⍳⎕C 3↑⍵]
        token rest←3(↑{⍺⍵}↓)⍺
        'MMM' 'Mmm' 'mmm' '_mm'∊⍨⊂token:(1 2⍴'M' (months⍳⎕C 3↑⍵))⍪rest∇⍵↓⍨3
        'DDD' 'Ddd' 'ddd' '_dd'∊⍨⊂token:(1 2⍴'Dw'(dows⍳⎕C 3↑⍵))  ⍪rest∇⍵↓⍨3
        token rest←2(↑{⍺⍵}↓)⍺
        'YY'          ≡  token:(1 2⍴'Y0'(⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'MM' '_M'     ∊⍨⊂token:(1 2⍴'M' (⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'DD' '_D'     ∊⍨⊂token:(1 2⍴'D' (⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'OO' 'Oo' 'oo'∊⍨⊂token:(1 2⍴'Do'(ord⍳⎕C 2↑⍵)) ⍪rest∇⍵↓⍨2
        'yy' '_y'     ≡  token:(1 2⍴'Dy'(⍎3↑⍵))       ⍪rest∇⍵↓⍨3
        'WW'          ≡  token:(1 2⍴'Yw'(⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'ww' '_w'     ∊⍨⊂token:(1 2⍴'Ww'(⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'tt' '_t'     ∊⍨⊂token:(1 2⍴'hm'(⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'PP' 'pp'     ∊⍨⊂token:(1 2⍴'hq'(half⍳⎕C 2↑⍵))⍪rest∇⍵↓⍨2
        'hh' '_h'     ∊⍨⊂token:(1 2⍴'h' (⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'mm' '_m'     ∊⍨⊂token:(1 2⍴'m' (⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        'ss' '_s'     ∊⍨⊂token:(1 2⍴'s' (⍎2↑⍵))       ⍪rest∇⍵↓⍨2
        token rest←1(↑{⍺⍵}↓)⍺ ⋄ token←⊃token
        'M' ≡  token:(1 2⍴'M' (⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 12)∊⍨⊂2↑⍵
        'D' ≡  token:(1 2⍴'D' (⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 31)∊⍨⊂2↑⍵
        'y' ≡  token:(1 2⍴'Dy'(⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1+(⍵[2]∊⎕D)+(⍕¨99↓⍳365)∊⍨⊂3↑⍵
        'w' ≡  token:(1 2⍴'Ww'(⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 53)∊⍨⊂2↑⍵
        'd' ≡  token:(1 2⍴'Dw'(⍎  1↑⍵))    ⍪rest∇⍵↓⍨1
        'Oo'∊⍨⊂token:(1 2⍴'Do'(ords⍳⎕C⊃⍵)) ⍪rest∇⍵↓⍨1
        't' ≡  token:(1 2⍴'hm'(12|⍎len↑⍵)) ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 12)∊⍨⊂2↑⍵
        'Pp'∊⍨⊂token:(1 2⍴'hq'(halfs⍳⎕C⊃⍵))⍪rest∇⍵↓⍨1
        'h' ≡  token:(1 2⍴'h' (⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 23)∊⍨⊂2↑⍵
        'm' ≡  token:(1 2⍴'m' (⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 59)∊⍨⊂2↑⍵
        's' ≡  token:(1 2⍴'s' (⍎len↑⍵))    ⍪rest∇⍵↓⍨len←1          +(⍕¨ 9↓⍳ 59)∊⍨⊂2↑⍵
        ⍺ ∇⍥(1∘↓) ⍵
    } ⍵
⍝   key:
⍝     Y0  100|year                                            YY
⍝     Y   year            YYYY
⍝     M   month           MMMM Mmmm mmmm _mmm MMM Mmm mmm _mm MM _M    M
⍝     D   day (of month)                                      DD _D    D
⍝     Do  " st nd rd th                                       OO Oo oo O o
⍝     Dy  day of year                                         yy _y    y
⍝     --
⍝     Yw  week-year       WWWW                                WW
⍝     Ww  week of year                                        ww _w    w
⍝     Dw  day of week     DDDD Dddd dddd _ddd DDD Ddd ddd _dd          d
⍝     --
⍝     hm  12|hour                                             tt _t    t
⍝     hq  am/pm                                               PP pp    P p
⍝     h   hour                                                hh _h    h
⍝     m   minute                                              mm _m    m
⍝     s   second                                              ss _s    s
    group ←parse[;1]{⍺,(⊃⍵),(≢⍵)}⌸parse[;2]
    1<⌈/group[;3]:'inconsistent input'⎕SIGNAL 7
    fmask ←'hmstP'∊⍨⊃¨group[;1]
    frac  ←' '0 0⍪group⌿⍨ fmask
    fracp ←frac[;1]{
        pick  ←⍺{⍵⍵/⍨⍵∘≡¨⍺⍺}⍵
        second←⊃pick's'
        minute←⊃pick'm'
        hourq ←(1-⍨pick'hq')∪(⌊12÷⍨pick'h')
        1<≢hourq:'inconsistent hour'⎕SIGNAL 7
        hourq ←⊃hourq
        hourm ←(pick'hm')   ∪(12|pick'h')
        1<≢hourm:'inconsistent hour'⎕SIGNAL 7
        hourm ←⊃hourm
        2 12 60 60⊥hourq hourm minute second
    }frac[;2]
    fracp÷←86400
    int   ←' '0 0⍪group⌿⍨~fmask
    intp  ←int[;1]{
        pick ←⍺{⍵⍵/⍨⍵∘≡¨⍺⍺}⍵
        yearm←(pick'Y0')∪(100|pick'Y')
        yearq←⌊100÷⍨pick'Y'
        month←pick'M'
        day  ←pick'D'
        dayo ←pick'Do'
        dayy ←pick'Dy'
        yearw←pick'Yw'
        weekw←pick'Ww'
        dayw ←7|pick'Dw'
⍝⍝            subyear restrictions
⍝             leap regular
        ym   ←(⍳366) (⍳365)
	mm   ←((1+@2⊢monthd)/⍳12) (monthd/⍳12)
        dm   ←(∊⍳¨1+@2⊢monthd) (∊⍳¨monthd)
        ymf  ←(∧/∘.=∘dayy)¨ym
        mmf  ←(∧/∘.=∘month)¨mm
        dmf  ←(∧/∘.=∘day)¨dm
        dmf ∧←∊∘(∊dayo⌷1 2 3(0 4 5 6 7 8 9))¨10|dm
        ymdf ←ymf∧mmf∧dmf
⍝             leap_reg reg_reg reg_leap
⍝             1 2 3 4 5 6 7 start
        yo   ←1-⍨2 1/(⍳365) (⍳366)
        ww   ←1+(↑1 2/(1+@5 6⊢7⍴52) (1+@5⊢7⍴52))|1-⍨(⌈7÷⍨yo∘.+⍳7)-⍤1⊢4 3/0 1
        dw   ←7|yo∘.+⍳7
        wwf  ←(∧/∘.=∘weekw)¨ww
        dwf  ←(∧/∘.=∘dayw)¨dw
        ywdf ←wwf∧dwf
⍝⍝            year restrictions
⍝             400 gregorian year cycles
        leap ←1@1⊢400⍴0,0=4|⍳99
        leapf←ymdf[2-leap]
        dif  ←leap-¯1⌽leap
        ydow ←1+7|+\5,1+¯1↓leap
        diff ←ywdf[ydow⊂⍤,⍤0⍨2+dif]
        daysf←leapf∧diff
⍝             year info
        yrmf ←400⍴∧/(1-⍨⍳100)∘.=yearm
        yrqf ←100/∧/0 1 2 3∘.=4|yearq
        yrf  ←yrmf∧yrqf

⍝⍝            compose
        valid←⍸∊yrf∧daysf
        ⍬≡valid:'inconsistent date'⎕SIGNAL 7
	yrqq ←⊃5,⍨⌊yearq÷4
        0 146097⊥yrqq (⊃valid)
    }int[;2]
    offset←693961
    fracp+intp-offset
}
