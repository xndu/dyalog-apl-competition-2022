runs←{∨/≠/(⍳⍺)∘.<+\⍵}
⍝                +\⍵    min,length -> min,max
⍝         (⍳⍺)∘.<       for each pos,row: <min, <max.
⍝       ≠/              -> <min ≠ <max (≡ in [min,max))
⍝     ∨/                for some run

fill←{⊃∘⌽∘⍸⍤1⊢∧/¨≠/(⍳⍺)∘.<(+\⍵(↑{⍺ ⍵}↓)⍤1⍨≢⍺)}
⍝                            ⍵(↑{⍺ ⍵}↓)⍤1⍨≢⍺    split each row into [min],[length]
⍝                          +\                   -> [min],[max]
⍝                  (⍳⍺)∘.<                      for each pos,row: <min, <max
⍝                ≠/                               unequal (≡ in [min,max))
⍝             ∧/¨                                 in every dimension (∧.≠/ fails for 0=≢⍵)
⍝          ⍤1                                   for each cell:
⍝     ⊃∘⌽∘⍸                                       last ⍵-row where present

subspaces←{1+⍪⍉(⍴⍵)⊤↑{-⍨\(1-⍨⍺⍳⍵)(-⍵⍳⍨⌽⍺)}∘(⍳⌈/)⍨,⍵}
⍝                                                ,⍵    ravel (=> ⍺)
⍝                                           ⍳⌈/        range of subspaces (=> ⍵)
⍝                            ⍺⍳⍵  -⍵⍳⍨⌽⍺                first pos, last pos of each subspace
⍝                         1-⍨                            subtract 1 from first (0 indexing)
⍝                     -⍨\                               min,max -> min,length
⍝             ⍉(⍴⍵)⊤↑                                  un-ravel index
⍝          1+⍪                                         shape and add 1 (1 indexing)
⍝
⍝ for fun, a golfed answer:
⍝ {↑¯1(⊢,⍥⊃⌽-+)¨⍸⍤=∘⍵¨⍳⌈/,⍵}
