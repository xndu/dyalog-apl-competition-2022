Ballot←{,⍨∘≢⌸0⌈1+(?⍨∘|-∘?1⌈-)¨⍵⍴⍺}
                  ?⍨∘|       ¨⍵⍴⍺   ⍝ random permutations of ⍳|⍺
               1+     -∘?1⌈-        ⍝ subtract random in 1-⍨⍳-⍺ when ⍺<0
             0⌈                     ⍝ no less than 0
        ,⍨∘≢⌸                       ⍝ group by permutation into count,permutation

IRV←{
    total←+⌿↑⍵[;1]×1=⍵[;2]              ⍝ votes garnered by each candidate
    ~∨/in←×total:⍬                      ⍝ no one has votes: no winner
    cands←in/⍳≢total ⋄ total/⍨←in
    tally←⊂cands,[1.5]total             ⍝ current round's vote tally
    ≢win←cands/⍨(+/<2×⊢)total:tally,win ⍝ if someone won, output tally,winner
    los←cands/⍨total=⌊/total            ⍝ all with lowest #votes lose the round
    next←{0@los⊢⍵-+/⍵∘.>⍵[los]~0}¨⍵[;2]
⍝                       ⍵[los]~0          for losers voted for,
⍝               ⍵-+/⍵∘.>                  shift up lower choices
⍝         0@los⊢                          remove losers from consideration
    tally,∇ next{⍺,⍨+/⍵}⌸⍵[;1]          ⍝ re-group voters and recurse, prepending tally
}
